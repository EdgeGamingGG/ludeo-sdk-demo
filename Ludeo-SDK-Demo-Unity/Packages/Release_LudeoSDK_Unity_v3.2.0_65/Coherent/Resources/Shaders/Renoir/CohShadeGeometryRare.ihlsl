/*
This file is part of Renoir, a modern graphics library.

Copyright (c) 2012-2016 Coherent Labs AD and/or its licensors. All
rights reserved in all media.

The coded instructions, statements, computer programs, and/or related
material (collectively the "Data") in these files contain confidential
and unpublished information proprietary Coherent Labs and/or its
licensors, which is protected by United States of America federal
copyright law and by international treaties.

This software or source code is supplied under the terms of a license
agreement and nondisclosure agreement with Coherent Labs AD and may
not be copied, disclosed, or exploited except in accordance with the
terms of that agreement. The Data may not be disclosed or distributed to
third parties, in whole or in part, without the prior written consent of
Coherent Labs AD.

COHERENT LABS MAKES NO REPRESENTATION ABOUT THE SUITABILITY OF THIS
SOURCE CODE FOR ANY PURPOSE. THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT
HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES,
INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
MERCHANTABILITY, NONINFRINGEMENT, AND FITNESS FOR A PARTICULAR PURPOSE
ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER, ITS AFFILIATES,
PARENT COMPANIES, LICENSORS, SUPPLIERS, OR CONTRIBUTORS BE LIABLE FOR
ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS
OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN
ANY WAY OUT OF THE USE OR PERFORMANCE OF THIS SOFTWARE OR SOURCE CODE,
EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/
#ifndef COH_SHADE_GEOMETRY_RARE_GUARD
#define COH_SHADE_GEOMETRY_RARE_GUARD

#define SHOW_DF 0
#define INVALID_UV_SCISSOR -1.0f

float GetLuminance(float3 color)
{
	return 0.2126f * color.r + 0.7152f * color.g + 0.0722f * color.b;
}


void ShadeGeometry(in PS_INPUT input, inout float4 outColor, inout float alpha)
{
	// Circle / Rounded rect
	if (ShaderType == 1)
	{
		const float2 posPixels = input.ScreenNormalPosition.xy;
		const float distance2edge = length(posPixels - input.Additional.xy) - input.Additional.z;
		alpha = clamp(0.5 - distance2edge, 0, 1);
	}
	// Stroke Circle / Rounded rect
	else if (ShaderType == 2)
	{
		const float2 posPixels = input.ScreenNormalPosition.xy;
		const float de = length(posPixels - input.Additional.xy);
		const float distance2OuterEdge = de - input.Additional.z;
		const float distance2InnerEdge = de - (input.Additional.z - input.Additional.w);
		alpha = clamp(0.5 - distance2OuterEdge, 0, 1);
		alpha *= 1 - clamp(0.5 - distance2InnerEdge, 0, 1);
	}
	// Ellipse
	else if (ShaderType == 4)
	{
		const float2 offset = (input.ScreenNormalPosition.xy - input.Additional.xy) * rcp(input.Additional.zw);
		const float test = dot(offset, offset) - 1.0;
		const float2 dudx = ddx(offset);
		const float2 dudy = ddy(offset);
		const float2 gradient = float2(2.0 * offset.x * dudx.x + 2.0 * offset.y * dudx.y,
										2.0 * offset.x * dudy.x + 2.0 * offset.y * dudy.y);
		const float grad_dot = max(dot(gradient, gradient), 1.0e-4);
		const float invlen = rsqrt(grad_dot);

		alpha = saturate(0.5 - test * invlen);
	}
	// Stroke Ellipse
	else if (ShaderType == 5)
	{
		float2 offset = (input.ScreenNormalPosition.xy - input.Additional.xy) * rcp(input.Additional.zw + (PrimProps0.x / 2.0));
		float test = dot(offset, offset) - 1.0;
		float2 dudx = ddx(offset);
		float2 dudy = ddy(offset);
		float2 gradient = float2(2.0 * offset.x * dudx.x + 2.0 * offset.y * dudx.y,
								2.0 * offset.x * dudy.x + 2.0 * offset.y * dudy.y);
		float grad_dot = max(dot(gradient, gradient), 1.0e-4);
		float invlen = rsqrt(grad_dot);

		alpha = saturate(0.5 - test * invlen);

		offset = (input.ScreenNormalPosition.xy - input.Additional.xy) * rcp((input.Additional.zw - (PrimProps0.x / 2.0)));
		test = dot(offset, offset) - 1.0;
		dudx = ddx(offset);
		dudy = ddy(offset);
		gradient = float2(2.0 * offset.x * dudx.x + 2.0 * offset.y * dudx.y,
						2.0 * offset.x * dudy.x + 2.0 * offset.y * dudy.y);
		grad_dot = max(dot(gradient, gradient), 1.0e-4);
		invlen = rsqrt(grad_dot);

		alpha *= saturate(0.5 + test * invlen);
	}
	// Blur
	else if (ShaderType == 6)
	{
		outColor = float4(0, 0, 0, 0);
#if !defined(__DX9__) && !defined(__DX11_LEVEL_9_3__) && !defined(__GLES_100__)
		const uint stepsCount = uint(PrimProps0.x);
		for (uint i = 0; i < stepsCount; ++i)
#else
		for (int i = 0; i < PrimProps0.x; ++i)
#endif
		{
			float coeff = Coefficients[i / 4][i % 4];
			float2 offset;
			offset.x = PixelOffsets[(i*2) / 4][(i*2) % 4];
			offset.y = PixelOffsets[(i*2 + 1) / 4][(i*2 + 1) % 4];

			float4 uvPointWithOffset = float4(input.Additional.xy + offset, 0, 0);
			float4 uvPointWithNegativeOffset = float4(input.Additional.xy - offset, 0, 0);
			if (PrimProps1.z != INVALID_UV_SCISSOR || PrimProps1.w != INVALID_UV_SCISSOR)
			{
				uvPointWithOffset.x = clamp(input.Additional.x + offset.x, PrimProps1.x, PrimProps1.x + PrimProps1.z);
				uvPointWithOffset.y = clamp(input.Additional.y + offset.y, PrimProps1.y, PrimProps1.y + PrimProps1.w);

				uvPointWithNegativeOffset.x = clamp(input.Additional.x - offset.x, PrimProps1.x, PrimProps1.x + PrimProps1.z);
				uvPointWithNegativeOffset.y = clamp(input.Additional.y - offset.y, PrimProps1.y, PrimProps1.y + PrimProps1.w);
			}

			outColor += coeff
				* (SAMPLE2D_LOD(txBuffer, uvPointWithOffset)
				+ SAMPLE2D_LOD(txBuffer, uvPointWithNegativeOffset));
		}
		alpha = input.Color.a;
	}
	// Image with color matrix
	else if (ShaderType == 7)
	{
		float2 uvs = input.Additional.xy;
		if (PrimProps1.z != INVALID_UV_SCISSOR || PrimProps1.w != INVALID_UV_SCISSOR)
		{
			uvs.x = clamp(uvs.x, PrimProps1.x, PrimProps1.x + PrimProps1.z);
			uvs.y = clamp(uvs.y, PrimProps1.y, PrimProps1.y + PrimProps1.w);
		}

		float4 baseColor = SAMPLE2D(txBuffer, uvs);
		float nonZeroAlpha = max(baseColor.a, 0.00001);
		baseColor = float4(baseColor.rgb / nonZeroAlpha, nonZeroAlpha);

		// TODO: Rename the members of the constant buffer so they are not weird for non-blurs
		outColor.r = dot(baseColor, Coefficients[0]);
		outColor.g = dot(baseColor, Coefficients[1]);
		outColor.b = dot(baseColor, Coefficients[2]);
		outColor.a = dot(baseColor, PixelOffsets[0]);
		outColor += PixelOffsets[1];

		outColor.a = lerp(GetLuminance(outColor.rgb), outColor.a, input.Color.b);

		alpha = outColor.a * input.Color.a * saturate(input.Additional.z);
		outColor.a = 1;
	}
	// YUV2RGB (9) YUVA2RGB (12)
	else if (ShaderType == 9 || ShaderType == 12)
	{
		float3 YCbCr;
		YCbCr.x = SAMPLE2D(txBuffer, input.Additional.xy) COH_A8_SAMPLE_MASK;
		YCbCr.y = SAMPLE2D(txBuffer1, input.Additional.xy) COH_A8_SAMPLE_MASK;
		YCbCr.z = SAMPLE2D(txBuffer2, input.Additional.xy) COH_A8_SAMPLE_MASK;

		YCbCr -= float3(0.0625, 0.5, 0.5);
		float3x3 yuv2rgb = float3x3(float3(1.164, 0, 1.596), float3(1.164, -0.391, -0.813), float3(1.164, 2.018, 0));
		float3 rgb = mul(yuv2rgb, YCbCr);

		alpha = input.Color.a * saturate(input.Additional.z);
		outColor = float4(rgb, 1.0f);

		if (ShaderType == 12)
		{
			float a = SAMPLE2D(txBuffer3, input.Additional.xy) COH_A8_SAMPLE_MASK;
			alpha *= a;
		}
	}
	// Hairline
	else if(ShaderType == 11)
	{
		const float3 posPixels = float3(input.ScreenNormalPosition.xy, 1);
		const float distance2line = abs(dot(input.Additional.xyz, posPixels));
		// Multiply by the scale (Additional.w) to account for thickness
		// Note that this calculation only works for scale (Additional.w) LEQ 1 so saturate at the end
		alpha = saturate((1 - saturate(distance2line)) * input.Additional.w);
	}
	else if (ShaderType == 19)
	{
		// SDF Text (outline)
		float dfValue = SAMPLE2D(txBuffer, input.Additional.xy) COH_A8_SAMPLE_MASK;
	#if SHOW_DF
		outColor = float4(dfValue.xxx, 1);
		return;
	#endif
		const float scale = sqrt(PrimProps0.y * 0.5);

		const float bias = 0.5 * scale - 0.9;
		const float outlineWidth = PrimProps0.z / PrimProps0.y * 0.5 * scale;
		dfValue *= scale;

		float4 c = lerp(PrimProps1, input.Color, saturate(dfValue - (bias + outlineWidth)));

		c *= saturate(dfValue - max(0, bias - outlineWidth));
		outColor = c;
	}
	else if (ShaderType == 21)
	{
		// Generate SDF Stencil
		float2 par = input.Additional.xy;
		float val = float( par.x * par.x < par.y );

		// Using 'discard' here generates broken code when using the 2021.04 XSX compiler.
		// The workaround is to use the 'clip' intrinsic instead, which generates correct code.
		// The issue manifests itself by discarding pixels based on the above check,
		// but without actually checking if ShaderType==21, usually resulting in images
		// clipped with a curve resembling a quarter circle.
		// The issue is not present with the 2021.06 compiler.
		//if ( val == 0.0 )
		//	discard;
		clip(val == 0.0 ? -1 : 1);

		outColor = float4( 1.0, 1.0, 1.0, 1.0 );
	}
	else if (ShaderType == 23)
	{
		// Render SDF on GPU Text (outline)

		float dfValue = SAMPLE2D(txBuffer, input.Additional.xy) COH_SDF_GPU_SAMPLE_MASK;

	#if SHOW_DF
		outColor = float4(dfValue.xxx, 1);
		return;
	#endif

		const float dfDeltaPerScreenPixel = PrimProps0.x;
		const float halfSmooth = dfDeltaPerScreenPixel;
		const float strokeWidth = PrimProps0.z;

		const float dfStrokeWidth = strokeWidth * dfDeltaPerScreenPixel;
		float threshold = 0.5f + 0.5f * dfStrokeWidth; // Offset by half of the stroke width
		const float outerEdgeCenter = threshold - dfStrokeWidth;

		float4 fontColor = input.Color;
		float4 outlineColor = PrimProps1;

		float actualAlpha = smoothstep(outerEdgeCenter - halfSmooth, outerEdgeCenter + halfSmooth, dfValue);

		if (actualAlpha <= 0.0f)
			discard;

		float outlineToSolidQuotient = smoothstep(threshold - halfSmooth, threshold + halfSmooth, dfValue);
		outColor = lerp(outlineColor, fontColor, outlineToSolidQuotient);
		outColor = outColor * actualAlpha;
	}
	else if (ShaderType == 24)
	{
		// MSDF text

		float3 color = SAMPLE2D(txBuffer3, input.Additional.xy).xyz;

	#if SHOW_DF
		outColor = float4(color.xyz, 1);
		return;
	#endif

		float dfDeltaPerScreenPixel = input.Additional.z;
		const float halfSmooth = dfDeltaPerScreenPixel;

		float threshold = 0.5f;
		float dfValue = max(min(color.r, color.g), min(max(color.r, color.g), color.b));
		dfValue = smoothstep(threshold - halfSmooth, threshold + halfSmooth, dfValue);

		outColor = input.Color * dfValue;
	}
	else if (ShaderType == 25)
	{
		// Render MSDF Text (outline)

		float3 color = SAMPLE2D(txBuffer, input.Additional.xy).rgb;

	#if SHOW_DF
		outColor = float4(color.xyz, 1);
		return;
	#endif

		const float dfDeltaPerScreenPixel = PrimProps0.x;
		const float halfSmooth = dfDeltaPerScreenPixel;
		const float strokeWidth = PrimProps0.z;

		const float dfStrokeWidth = strokeWidth * dfDeltaPerScreenPixel;
		float threshold = 0.5f + 0.5f * dfStrokeWidth; // Offset by half of the stroke width
		const float outerEdgeCenter = threshold - dfStrokeWidth;

		float4 fontColor = input.Color;
		float4 outlineColor = PrimProps1;

		float dfValue = max(min(color.r, color.g), min(max(color.r, color.g), color.b));
		float actualAlpha = smoothstep(outerEdgeCenter - halfSmooth, outerEdgeCenter + halfSmooth, dfValue);

		if (actualAlpha <= 0.0f)
			discard;

		float outlineToSolidQuotient = smoothstep(threshold - halfSmooth, threshold + halfSmooth, dfValue);
		outColor = lerp(outlineColor, fontColor, outlineToSolidQuotient);
		outColor = outColor * actualAlpha;
	}

}

#endif // COH_SHADE_GEOMETRY_RARE_GUARD
